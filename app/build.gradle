plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'androidx.navigation.safeargs.kotlin'
    id 'com.google.protobuf'
}

android {
    compileSdkVersion 30
    buildToolsVersion "30.0.3"

    defaultConfig {
        applicationId "org.skyfaced.todi"
        minSdkVersion 21
        targetSdkVersion 30
        versionCode 1
        versionName "1.0.0-alpha"

        javaCompileOptions {
            annotationProcessorOptions {
                arguments += [
                        "room.schemaLocation":"$projectDir/schemas".toString(),
                        "room.incremental":"true",
                        "room.expandProjection":"true"
                ]
            }
        }
    }

    buildTypes {
        debug {
            debuggable true
        }

        release {
            debuggable false
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8.toString()
    }

    buildFeatures {
        viewBinding true
    }

    sourceSets {
        main {
            proto {
                srcDir 'src/main/proto'
            }
            java {
                srcDirs 'build/generated/source/proto/main/java'
            }
        }
    }
}

protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:$protobufVersion"
    }

    generateProtoTasks {
        all().each { task ->
            task.builtins {
                java {
                    option "lite"
                }
            }
        }
    }
}

dependencies {
    //LeakCanary
    debugImplementation "com.squareup.leakcanary:leakcanary-android:$leakCanaryVersion"

    //UI
    implementation "com.google.android.material:material:$materialVersion"
    implementation "androidx.swiperefreshlayout:swiperefreshlayout:$refreshLayoutVersion"
    implementation "androidx.constraintlayout:constraintlayout:$constraintLayoutVersion"

    //Navigation
    implementation "androidx.navigation:navigation-fragment-ktx:$navigationVersion"
    implementation "androidx.navigation:navigation-ui-ktx:$navigationVersion"

    //ViewBinding
    implementation "com.github.kirich1409:viewbindingpropertydelegate-noreflection:$viewBindingVersion"

    //DI
    implementation "io.insert-koin:koin-android:$koinVersion"

    //Markdown
    implementation "io.noties.markwon:editor:$markwonVersion"
    implementation "io.noties.markwon:ext-strikethrough:$markwonVersion"
    implementation "io.noties.markwon:ext-tasklist:$markwonVersion"
    implementation "io.noties.markwon:ext-tables:$markwonVersion"

    //Room
    implementation "androidx.room:room-runtime:$roomVersion"
    implementation "androidx.room:room-ktx:$roomVersion"
    kapt "androidx.room:room-compiler:$roomVersion"

    //Paging
    implementation "androidx.paging:paging-runtime-ktx:$pagingVersion"

    //Lifecycle
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:$lifecycleVersion"

    //Datastore
    implementation "com.google.protobuf:protobuf-javalite:$protobufVersion"
    implementation "androidx.datastore:datastore:$dataStoreVersion"

    //Other
    implementation "androidx.core:core-ktx:$coreVersion"
    implementation "androidx.appcompat:appcompat:$appCompatVersion"
    implementation "androidx.preference:preference-ktx:$preferenceVersion"
}